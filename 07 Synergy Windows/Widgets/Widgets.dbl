
;;; <summary>
;;; The main entry point for the application.
;;; </summary>
main Widgets

	.include "WND:windows.def"

	record header
		oh_number       ,d6         ;Order number
		oh_customer     ,a8         ;Customer account code
		oh_date         ,d8         ;Order date
		oh_contact      ,a20        ;Order placed by
		oh_salesrep     ,d4         ;Sales rep code
	endrecord

	structure strDetail
		od_number       ,d6         ;Order number
		od_item         ,d4         ;Item number
		od_part         ,a10        ;Part number
		od_qty          ,d5         ;Quantity ordered
		od_unit_price   ,d8.2       ;Unit price
	endstructure

	record
		detail          ,strDetail  ;Order detail structure field
	endrecord

	record
		system_date     ,a20
		year          ,d4 @system_date
		month         ,d2 @system_date+4
		day           ,d2 @system_date+6
	endrecord

	record
		termch          ,i4 ,1      ;Terminal channel number
		char            ,a1         ;Temporary variable used to accept key press
		msgid           ,i4         ;Message window ID
	endrecord

proc

	call startup
	call process
	call shutdown

	stop

;----------------------------------------
;Application startup processing
;
Startup,

	system_date = %datetime                         ;Assign current date

	open(termch,i,"tt:")                            ;Open terminal channel
	xcall flags(7004020,1)                          ;Setup environment
	display(termch,$scr_clr(screen))                ;Clear screen

	xcall w_init(1,termch,5)                        ;Initialize windowing system
	xcall w_caption(WC_SET,"My First Synergy Application")  ;Set caption

	return

;----------------------------------------
;Application shutdown processing
;
Shutdown,

	xcall w_exit                                    ;Close windowing system

	display(termch,$scr_clr(screen))                ;Clear screen
	close termch                                    ;Close terminal

	return

;----------------------------------------
;Main application processing
;
Process,

	xcall w_proc(WP_CREATE,msgid,"MESSAGE",4,25,    ;Create window
	&	WP_PLACE,msgid,9,30)               ;Place window
	xcall w_brdr(msgid,WB_TPOS,WBT_TOP,WBT_CENTER,WB_TITLE,"Message")
	xcall w_disp(msgid,WD_POS,2,8,"Hello World!",   ;Display text
	&	WD_POS,3,3,"The date is ",
	&	^a(month),"/",^a(day),"/",^a(year))
	xcall w_disp(msgid,WD_ACCEPT,char)              ;Wait for key
	xcall w_proc(WP_DELETE,msgid)                   ;Delete window

	return

endmain